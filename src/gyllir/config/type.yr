in type;

use std::{config::_, stream};

/**
 * Enum defining the type of the target
 * */
enum
| EXECUTABLE = (0u8, "executable")
| LIBRARY    = (1u8, "library")
 -> BuildTypeEnum;

/**
 * Record used to transform configuration into a build type
 * */
pub record BuildType {

    // The type of the target to build
    let mut _type : u8 = BuildTypeEnum::EXECUTABLE.0;

    /**
     * executable target
     * */
    pub self () {}

    /**
     * @params:
     *    - type: a string defining the type of the target
     * */
    pub self (type : [c8])
        throws std::conv::errors::CastFailure
    {
        self:.parseType (type);
    }

    /**
     * Transform a configuration into a build type
     * */
    pub self (cfg : &Config)
        throws std::conv::errors::CastFailure, AssertError
    {
        if let Str (value-> type) = cfg {
            self:.parseType (type);
        }

        else throw copy AssertError ("Expecting a string");
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    pub fn isExecutable (self)-> bool {
        self._type == BuildTypeEnum::EXECUTABLE.0
    }

    pub fn isLibrary (self)-> bool {
        self._type == BuildTypeEnum::LIBRARY.0
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          MISC          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Streamable;
    impl Serializable {
        pub over serialize (self)-> &Config {
            for i in BuildTypeEnum::__members__ {
                if i.0 == self._type {
                    return copy Str (i.1);
                }
            }

            panic;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          PRIVATE          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Parse the type from a string
     * */
    prv fn parseType (mut self, value : [c8])
        throws std::conv::errors::CastFailure
    {
        for i in BuildTypeEnum::__members__ {
            if i.1 == value {
                self._type = i.0;
                return;
            }
        }

        throw copy std::conv::errors::CastFailure (([c8])::__typeinfo__, BuildType::__typeinfo__);
    }
}
