in dependency;

use std::{config::_, stream};
use gyllir::config::{url, version, version::filter};

/**
 * A dependency to an external package
 * */
pub record Dependency {

    // The version of the dependency package
    let _version : VersionFilter;

    // The url of the dependency
    let _url : Url;

    pub self (version : VersionFilter, url : Url)
        with _version = version
        , _url = url
    {}

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    @field
    pub fn version (self)-> VersionFilter {
        self._version
    }

    @field
    pub fn url (self)-> Url {
        self._url
    }

}

/**
 * A dependency list is a map of Dependency, that can be serialized into configuration
 */
pub class DependencyList {

    /**
     * The list of dependencies
     */
    let mut _package : [[c8] => mut Dependency] = [];

    /**
     * Create an empty dependency list
     */
    pub self () {}

    /**
     * Create a dependency list from a map of Dependency
     */
    pub self (mut elem : [[c8] => mut Dependency])
        with _package = alias elem
    {}

    /**
     * Create a dependency list from a configuration
     * This constructor can be used by the std for serialization
     * @example: 
     * =================
     * def getConfiguration ()-> &Config;
     *
     * let dmut list = getConfiguration ().to!{&DependencyList} ();
     * =================
     */
    pub self (cfg : &Config)
        throws AssertError, std::conv::errors::CastFailure
    {
        if let d : &Dict = cfg {
            self:.insert (d);
        }

        else throw copy AssertError ("Expecting a dictionnary"s8);
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GET/SET          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Remove a dependency from the list
     * @params:
     *   - elem: the name of dependency to remove
     */
    pub fn remove (mut self, elem : [c8]) {
        self._package:.remove (elem);
    }

    /**
     * @returns: the list of dependencies
     */
    pub fn opIndex (self)-> [[c8] => Dependency] {
        self._package
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          MISC          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Streamable;
    impl Serializable {
        pub over serialize (self)-> &Config {
            let dmut res = copy Dict ();
            for k, d in self._package {
                let dmut dep = copy Dict ();
                dep:["version"] = d.version.serialize();
                dep:["url"] = d.url.serialize ();

                res:[k] = dep;
            }

            res
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          PRIVATE          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */


    /**
     * Insert the dependencies from a configuration dictionnary
     */
    prv fn insert (mut self, cfg : &Dict)
        throws std::conv::errors::CastFailure
    {
        {
            for k, v in cfg[] {
                let version = VersionFilter (v ["version"]);
                let url = Url (v ["url"]);

                self._package [k] = Dependency (version-> version, url);
            }
        } catch {
            inE => {
                throw copy std::conv::errors::CastFailure ((&Config)::__typeinfo__, Dependency::__typeinfo__, subError-> (inE)?);
            }
        }
    }


    

}
