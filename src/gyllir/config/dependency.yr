mod gyllir::config::dependency;

import std::config::_;
import std::collection::map;
import std::io;
import std::tokenizer;
import std::conv;

import gyllir::config::url;
import gyllir::config::version;

pub class RecursiveDependency over Exception {

    pub let msg : [c8];
    
    pub self (msg: [c8]) with msg = msg {}

    impl Streamable {

        pub over toStream (self, dmut stream : &StringStream) {
            self::super.toStream (alias stream);
        }
    }
}


/**
 * A dependency is a git repo
 */
pub struct
| version: &Version /// The version to use
| url: &Url // The url of the git repo
 -> Dependency;

/**
 * A dependency list is a map of Dependency, that can be serialized into configuration
 */
pub class DependencyList {

    /**
     * The list of dependencies
     */
    let dmut _package = HashMap!{[c32], Dependency}::new ();

    /**
     * Create an empty dependency list
     */
    pub self () {}

    /**
     * Create a dependency list from a map of Dependency
     */
    pub self (dmut elem : &HashMap!{[c32], Dependency}) with _package = alias elem            
    {}

    /**
     * Create a dependency list from a configuration
     * This constructor can be used by the std for serialization
     * @example: 
     * =================
     * def getConfiguration ()-> &Config;
     *
     * let dmut list = getConfiguration ().to!{&DependencyList} ();
     * =================
     */
    pub self (cfg : &Config)
        throws &AssertError, &ConfigError
    {
        match cfg {
            x : &Dict => {
                self:.insert (x);
            }
            _ => {
                throw AssertError::new ("Expecting a dictionnary");
            }
        }
    }

    /**
     * Insert the dependencies from a configuration dictionnary
     */
    prv def insert (mut self, elem : &Dict) throws &ConfigError
    {
        for i, j in elem {                
            {    
                self._package:.insert (i, j.to!Dependency())
            }
            catch {
                x : &ConfigError => {
                    throw x;
                }
            }            
        }
    }

    /**
     * Remove a dependency from the list
     * @params:
     *   - elem: the name of dependency to remove
     */
    pub def remove (mut self, elem : [c32]) {
        self._package:.remove(elem);
    }

    /**
     * @returns: the list of dependencies
     */
    pub def getList (self)-> &HashMap!{[c32], Dependency} {
        self._package
    }

    
    impl std::config::conv::Serializable {

        /**
         * Transform the list into a Configuration, that can be dumped
         */
        pub over serialize (self)-> &Config {
            let dmut d = Dict::new ();
            for i, j in self._package {
                d:.insert(i,j.to!(&Config)());
            }
            d
        }
    }

    impl Streamable;    
    impl core::duplication::Copiable;    
}
