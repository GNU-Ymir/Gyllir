in url;

use std::{config::_, fs::path, stream};
use std::algorithm::searching;

/**
 * There are two types of url for the moment
 * - Local ones, that are refering to file datas
 * - Git ones, that are refering to a git repository on a remote server
 */
pub enum
| LOCAL = 0u8
| GIT   = 2u8
 -> UrlType;

pub enum
| LOCAL = "local:"
| GIT   = "git:"
 -> UrlStarts;

/**
 * An url that is used to get refer to package repositories
 * Url seems to be a common object, maybe it has its place in the std, in the use std::net module
 */   
pub record Url {

    // The path of the url
    let mut _path : Path = Path ("");

    // The type of the url
    let mut _type : UrlType = UrlType::LOCAL;

    // True iif the url is valid
    let mut _valid : bool = false;

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          CTOR          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Create an empty url
     */
    pub self () {}

    /**
     * Create an url from a string
     */
    pub self (c : [c8]) {
        self:.parseStr (c);
        self._valid = true;
    }

    /**
     * Create an url from a configuration
     * This constructor can be used by the std for deserialization
     * @example: 
     * ====================
     * fn getConfiguration ()-> &Config;
     *
     * let dmut list = getConfiguration ().to!{&Url} (); 
     * ====================
     */
    pub self (cfg : &Config)
        throws AssertError
    {
        if let Ok (Str (value-> url)) = cfg ["local"]? {
            self._path = Path (url);
            self._type = UrlType::LOCAL;
            self._valid = true;
        }

        else if let Ok (Str (value-> url)) = cfg ["git"]? {
            self._path = Path (url);
            self._type = UrlType::GIT;
            self._valid = true;
        }


        else if let Str (value-> url)  = cfg {
            self:.parseStr (url);
            self._valid = true;
        }

        else throw copy AssertError ("malformed url");
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          GETTERS          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: the type of url
     */
    @field
    pub fn type (self)-> UrlType {
        self._type
    }

    /**
     * @returns: the path of the url
     */
    @field
    pub fn path (self)-> Path {
        self._path
    }

    /**
     * @returns: true if the url parsing succeded
     */
    pub fn isValid (self)-> bool {
        self._valid
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ===================================          COMPARISON          ===================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * @returns: true if self == o
     */
    pub fn opEquals (self, ref o : Url)-> bool {
        if (self._path != o._path) return false;
        if (self._type != o._type) return false;
        if (self._valid != o._valid) return false;

        true
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ====================================          PARSING          =====================================
     * ====================================================================================================
     * ====================================================================================================
     */

    /**
     * Parse an url from a string
     * @params:
     *    - url: the url to parse
     */
    prv fn parseStr (mut self, url : [c8]) {
        if startsWith (url, UrlStarts::LOCAL) {
            self._path = Path (url [UrlStarts::LOCAL.len .. $]);
            self._type = UrlType::LOCAL;
        }

        else if (startsWith (url, UrlStarts::GIT)) {
            self._path = Path (url [UrlStarts::GIT.len - 1us .. $]);
            self._type = UrlType::GIT;
        }

        else {
            self._path = Path (url);
            self._type = UrlType::GIT;
        }
    }

    /*!
     * ====================================================================================================
     * ====================================================================================================
     * ======================================          MISC          ======================================
     * ====================================================================================================
     * ====================================================================================================
     */

    impl Hashable;
    impl Streamable;
    impl Serializable {

        /**
         * Transform the url into a configuration
         */
        pub over serialize (self)-> &Config {
            let dmut d = copy Dict ();
            match self._type {                
                UrlType::LOCAL => {
                    d:["local"] = copy Str (self._path.toStr ());
                }
                _ => { d:["git"] = copy Str (self._path.toStr ()); }
            }

            d
        }
    }
        
}
