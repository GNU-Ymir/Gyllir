mod gyllir::args::init;
import std::args, std::syntax::lexer, std::io;
import gyllir::args::sub;

struct
| name : [c32]
| mut help : YesNo
 -> InitArgs;

/**
 * Sub command used to create a new repo
 * @example: 
 * =:bash===========
 * $ gyllir init my-awesome-project
 * =================
 */
pub class @final InitCommand over SubCommand {

    let mut _args = InitArgs ("", YesNo::YES);
    
    pub self () with super (false) {}

    /**
     * Constructor called by the argument parser
     */
    pub self (args : [[c32]]) with super (true)
        throws &HelpPrinted
    {
        let parser = ArgumentParser!(InitArgs)::new ()
            :.info!"help" (short-> "h", comments-> "print this help and exit", isHelp-> true)
            :.positional!"name" (comments-> "the name of the package to init");

        {
            self._args = parser.parse (["./usage"] ~ args);
        } catch {
            err : &ArgumentError => {
                println (parser.simpleUsage (cmd-> "gyllir init"));
                println (parser.diagnosis (err));
                throw HelpPrinted::new ();
            }
            _ : &HelpRequired => {
                println (parser.usage (cmd-> "gyllir init"));
                throw HelpPrinted::new ();
            }
        }
            
    }

    /**
     * @returns: the name of the repo to create
     */
    pub fn getName (self)-> [c32] {
        self._args.name
    }
      
}
