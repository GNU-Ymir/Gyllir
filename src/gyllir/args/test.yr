mod gyllir::args::test;
import std::args, std::syntax::lexer, std::io;
import gyllir::args::sub;
import etc::c::sysinfo;

struct
| verbose : YesNo
| j : i64 = cast!i64 (sysinfo::_yrt_get_nprocs ())
| mut help : YesNo
| dry : YesNo = YesNo::NO
| release : YesNo = YesNo::NO
 -> TestArgs;

/**
 * Sub command used to build and run the test
 * @example: 
 * =:bash===========
 * $ gyllir test 
 * =================
 */
pub class @final TestCommand over SubCommand {

    let mut _args = TestArgs (YesNo::NO, YesNo::YES);
    
    pub self () with super (false) {}

    /**
     * Constructor called by the argument parser
     */
    pub self (args : [[c32]]) with super (true)
        throws &HelpPrinted
    {
        let parser = ArgumentParser!(TestArgs)::new ()
            :.info!"verbose" (short-> "v", comments-> "verbose mode")
            :.info!"j" (comments-> "number of parallel threads to spawn")
            :.info!"dry" (comments-> "only compile the test executable, but does not run them")
            :.info!"release" (comments-> "compile test in release mode (debug by default)")
            :.info!"help" (short-> "h", comments-> "print this help and exit", isHelp-> true);
        
        {
            self._args = parser.parse (["./usage"] ~ args);
        } catch {
            err : &ArgumentError => {
                println (parser.simpleUsage (cmd-> "gyllir test"));
                println (parser.diagnosis (err));
                throw HelpPrinted::new ();
            }
            _ : &HelpRequired => {
                println (parser.usage (cmd-> "gyllir test"));
                throw HelpPrinted::new ();
            }
        }
            
    }

    /**
     * @returns: true iif the option --verbose is set
     */
    pub def isVerbose (self)-> bool {
        self._args.verbose
    }

    /**
     * @returns: false iif the option --release is set
     * */
    pub def isDebug (self)-> bool {
        !self._args.release
    }

    /**
     * @returns: the number of thread to spawn 
     */
    pub def getNbThreads (self)-> u32 {
        cast!u32 (self._args.j)
    }

    /**
     * @returns: true iif test must not be executed
     */
    pub def isDry (self)-> bool {
        self._args.dry
    }
    
}
