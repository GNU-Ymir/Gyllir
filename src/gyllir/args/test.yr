mod gyllir::args::test;
import std::args, std::lexer, std::io;
import gyllir::args::sub;

struct
| verbose : YesNo
| mut help : YesNo
 -> TestArgs;

/**
 * Sub command used to create a new repo
 * @example: 
 * =:bash===========
 * $ gyllir test my-awesome-project
 * =================
 */
pub class @final TestCommand over SubCommand {

    let mut _args = TestArgs (YesNo::NO, YesNo::YES);
    
    pub self () with super (false) {}

    /**
     * Constructor called by the argument parser
     */
    pub self (args : [[c32]]) with super (true)
        throws &HelpPrinted
    {
        let parser = ArgumentParser!(TestArgs)::new ()
            :.info!"verbose" (short-> "v", comments-> "verbose mode")
            :.info!"help" (short-> "h", comments-> "print this help and exit", isHelp-> true);

        {
            self._args = parser.parse (["./usage"] ~ args);
        } catch {
            err : &ArgumentError => {
                println (parser.simpleUsage (cmd-> "gyllir test"));
                println (parser.diagnosis (err));
                throw HelpPrinted::new ();
            }
            _ : &HelpRequired => {
                println (parser.usage (cmd-> "gyllir test"));
                throw HelpPrinted::new ();
            }
        }
            
    }

    /**
     * @returns: true iif the option --verbose is set
     */
    pub def isVerbose (self)-> bool {
        self._args.verbose
    }

}
