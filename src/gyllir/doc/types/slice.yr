mod gyllir::doc::types::slice;

import gyllir::doc::types::base;
import std::config::_, std::io, std::conv;
import gyllir::utils::options;
import gyllir::doc::loader;


/**
 * A fixed size slice
 */
pub class SliceType over Type {

    // The inner type of the slice
    let _child : &Type;
    
    /**
     * @params: 
     *    - name: the name of the type ("slice")
     *    - isMut: true if the type is mutable
     *    - conf: the configuration of the slice type (json content)
     */
    pub self (name : [c8], isMut : bool, conf : &Config)
        with super (name-> name, isMut-> isMut), _child = {
            {
                loader::toType (conf["childs"][0])
            } catch {
                _ => Type::none ()
            }
        }
    {}

    /**
     * @returns: the type contained in the slice
     */
    pub def getInnerType (self)-> &Type {
        return self._child
    }

    impl Streamable;
    
}
