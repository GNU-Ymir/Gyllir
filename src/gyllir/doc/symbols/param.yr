mod gyllir::doc::symbols::param;

import std::io;
import std::config::_;

import gyllir::doc::symbols::_;
import gyllir::doc::types::_;
import gyllir::utils::options;
import gyllir::utils::string;
import gyllir::doc::loader;

/**
 * A function parameter
 */
pub class Parameter over Symbol {

    /// the type of the parameter
    let mut _type = Type::none ();

    /// True if the parameter is mutable
    let mut _mut : bool = false;

    /// True if the parameter is deeply mutable
    let mut _dmut : bool = false;
    
    /// True if the parameter is a reference
    let mut _ref : bool = false;

    /// The fnault value of the parameter
    let _value : [c8];

    pub self (config : &Config)
        with super (config),
    _value = (config ["value"]?).toOr (""s8)
    {
        match config ["type"]? {
            Ok (type : _) => { self._type = loader::toType (type); }
        }

        match config ["mut"]? {
            Ok (Str (str-> "true")) => { self._mut = true; }
            Ok (Bool (b-> true)) => { self._mut = true; }
            _ =>  { self._mut = false; }
        }

        match config["dmut"]? {
            Ok (Str (str-> "true")) => { self._dmut = true; }
            Ok (Bool (b-> true)) => { self._dmut = true; }
            _ =>  { self._dmut = false; }
        }

        match config ["ref"]? {
            Ok (Str (str-> "true")) => { self._ref = true; }
            Ok (Bool (b-> true)) => { self._ref = true; }
            _ => { self._ref = false; }
        }
    }


    /**
     * @returns: the value of the param
     */
    pub fn getValue (self)-> [c8] {
        self._value
    }

    /**
     * @returns: the value of the type
     */
    pub fn getType (self)-> &Type {
        self._type
    }

    /**
     * @returns: true iif the param is mutable
     */
    pub fn isMutable (self)-> bool {
        self._mut
    }

    /**
     * @returns: true iif the param is deeply mutable
     */
    pub fn isDeeplyMutable (self)-> bool {
        self._dmut
    }
    
    /**
     * @returns: true iif the param is a ref
     */
    pub fn isRef (self)-> bool {
        self._ref
    }
    
    impl Streamable;    
}
