in function;

use std::stream;
use gyllir::doc::types;

pub record Variable {

    pub let name : [c8];
    pub let type : &Type;
    pub let isMut : bool;
    pub let isRef : bool;
    pub let isLazy : bool;
    pub let defaultValue : [c8];

    /**
     * @params:
     *    - name: the name of the module
     *    - loc: the location of the module declaration
     *    - childs: the symbols found in the module
     * */
    pub self (name : [c8], type : &Type, isMut : bool, isRef : bool, isLazy : bool, defaultValue : [c8])
        with name = name
        , type = type
        , isMut = isMut
        , isRef = isRef
        , isLazy = isLazy
        , defaultValue = defaultValue
    {}

    impl Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            if self.isMut stream:.write ("mut ");
            if self.isRef stream:.write ("ref ");
            if self.isLazy stream:.write ("lazy ");

            stream:.write (self.name, " : ", self.type);
            if (self.defaultValue != "") stream:.write (" = ", self.defaultValue);
        }
    }
}


@final
pub class Function over Symbol {

    let _parameters : [Variable];

    let _retType : &Type;

    /**
     * @params:
     *    - name: the name of the module
     *    - loc: the location of the module declaration
     *    - childs: the symbols found in the module
     * */
    pub self (name : [c8], doc : [c8], loc : Location, protection : Protection, parameters : [Variable], retType : &Type)
        with super (name, doc, loc, protection)
        , _parameters = parameters
        , _retType = retType
    {}

    @field
    pub fn parameters (self)-> [Variable] {
        self._parameters
    }


    impl Streamable {
        pub over toStream (self, dmut stream : &StringStream) {
            stream:.write ("Function ", self._name, " => ", self._loc, "\n");
            stream:.entabing ();
            stream:.write ("fn (");
            for i, j in self._parameters {
                if i != 0 stream:.write (", ");
                stream:.write (j);
            }
            stream:.write (")-> ", self._retType, "\n");
            stream:.detabing ();
        }
    }
}
