mod gyllir::doc::dumper;

import std::fs::path;
import std::fs::file;
import std::config;
import std::config::json;
import std::io, std::conv;
import std::collection::vec;

import gyllir::doc::symbols::_;
import gyllir::doc::types::_;
import gyllir::doc::loader;
import gyllir::config::_;
import gyllir::doc::html::_;
import gyllir::repo::manager;

/**
 * This class is used to generate html files
 */
pub class DocDumper {

    /// the list of module doc.json files
    let _files : [&Path];

    /// The path of the directory that will get the documentations
    let _outDir : &Path;

    /// If true print verbose logs
    let _verbose = false;

    // The configuration of the repo
    let _config : GyllirConfig;

    // The context of the doc creator
    let dmut _context : &GyllirManager;
    
    /**
     * @params:
     *   - buildDir: the root of the building directory 
     *   - files: the list of files to parse
     *   - outDir: the directory in which dump the documentation
     *   - verbose: if true prints verbose logs
     */
    pub self (dmut context : &GyllirManager, conf : GyllirConfig, files : [&Path], outDir : &Path, verbose : bool = false)
        with  _files = files, _outDir = outDir, _verbose = verbose, _config = conf, _context = alias context
    {}

    /**
     * Generate the documentation
     */
    pub def generate (self) {
        let dmut tree = MetaModule::new ();
        let dmut modules = Vec!{&Module}::new ();
        
        for i in self._files {
            with dmut f = File::open (i) {

                let module = Module::new (json::parse (f:.readAll ()));
                tree:.insert (module);
                modules:.push (module);
            } catch {
                _ => println ("Failed to read : ", i);
            }
        }

        fs::removeDir (self._outDir, recursive-> true)?;
        fs::createDir (self._outDir, recursive-> true)?;

        let table = HtmlTable::new (tree);
        let head = HtmlHeader::new ("test"s8);
        let foot = HtmlFooter::new ();

        let dmut stream = StringStream::new ();
        head.dumpHtml (alias stream);
        table.dumpHtml (alias stream);
        foot.dumpHtml (alias stream);
        
        let ymir_res = Path::new (std::env::getEnv ("GYLLIR_HOME"s8)).push ("res"s8);        
        if (!self._context.runProcess ("cp"s8, ["-r"s8, ymir_res.push ("js"s8).toStr (), self._outDir.toStr ()])) {
            println ("[ERROR] Creating doc directory");            
        }

        if (!self._context.runProcess ("cp"s8, ["-r"s8, ymir_res.push ("css"s8).toStr (), self._outDir.toStr ()])) {
            println ("[ERROR] Creating doc directory");            
        }
        
        with dmut f = File::create (self._outDir.push ("index.html"s8), write-> true) {
            f:.write (stream[]);
        } catch {
            err : _ => {
                println ("[ERROR] Creating index file");
                println (err);
            }
        }
        
    }

    
}
