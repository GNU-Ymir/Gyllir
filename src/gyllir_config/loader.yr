mod gyllir_config::loader;

import std::io;
import std::tokenizer;
import std::config::toml;
import std::fs::_, std::env;
import std::config;
import std::conv;
import std::config::conv;
import gyllir_config::file_struct;

aka __FILENAME__ = "gyllir.toml"s8;
aka __GITIGNORE_FILENAME__ = ".gitignore"s8;
aka __DEFAULT_MAIN_CONTENT__ = str8#{mod main;
import std::io;
    
def main () {
    println ("Hello World !!");
}
};

aka __DEFAULT_IGNORE_CONTENT__ = str8#{
.build
__doc        
};        

aka __DEFAULT_MAIN_FILE__ = "main.yr"s8;
    
pub def init (name : [c8], author : [c8], license : [c8], description : [c8], type : [c8]) -> Content {
    return Content (name-> name,
                    version-> Version::new (),
                    author-> author,
                    dependencies-> DependenciesList::new (),
                    license-> license,
                    description-> description,
                    type-> type);
}

pub def load () -> (Content?) {
    with dmut f = File::open (Path::new (__FILENAME__)) {
        let str = f:.readAll ();
        return (parse (str).to!Content ())?;            
    } catch {
        _ => { // on catch tout
        }
    }
    return ((Content)?)::_err_
}

pub def createRepo (path : &Path, ct : Content)
    throws &FsError
{
    createRepo (path, ct.to!{&Config} (), ct)
}

prv def createRepo (path : &Path, cfg : &Config, ct : Content)
    throws &FsError
{
    fs::createDir (path, recursive-> true)?;
    saveToml(path,cfg);

    fs::createDir (path.push ("src"s8));
    with dmut f = File::create (path.push ("src"s8).push (__DEFAULT_MAIN_FILE__), write-> true) {
        f:.write (__DEFAULT_MAIN_CONTENT__);
    }

    with dmut f = File::create (path.push (__GITIGNORE_FILENAME__), write-> true) {
        f:.write (__DEFAULT_IGNORE_CONTENT__);
        f:.write (ct.name);
        f:.write ("\n"s8);
    }
}

pub def saveToml(path : &Path,ct : Content)
    throws &FsError
{
    saveToml(path,ct.to!{&Config} ())
}

pub def saveToml(path : &Path,cfg : &Config)
    throws &FsError
{
    with dmut f = File::create (path.push (__FILENAME__), write-> true) {
    let str = cfg.dump ().to![c8] ();
        f:.write (str);            
    }
}
